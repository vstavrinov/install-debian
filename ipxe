#!ipxe

:menu
menu Choose item:
item local Run install-debian script
item single-disk Debian Installer using preseeding (single disk /dev/sda)
item raid1 Debian Installer using preseeding (raid-1 /dev/sd{a,b})
item raid10 Debian Installer using preseeding (raid-10 /dev/sd{a,b,c,d})
item netboot.xyz Boot menu from http://netboot.xyz
item shell iPXE shell
choose option

iseq ${option} single-disk && goto preseed ||
iseq ${option} raid1 && goto preseed ||
iseq ${option} raid10 && goto preseed ||
goto ${option}

:local
imgfree
set URL https://b.link/ipxe/
iseq ${platform} efi && set initrd initrd=initrd.img ||
kernel ${URL}vmlinuz ${initrd} boot=live fetch=${URL}filesystem.squashfs
initrd ${URL}initrd.img
boot

:preseed
imgfree
set url http://deb.debian.org/debian/dists/stable/main/installer-amd64/current/images/netboot/debian-installer/amd64
set preseed_url https://raw.githubusercontent.com/vstavrinov/install-debian/master/preseed-
kernel ${url}/linux initrd=initrd.magic auto=true priority=critical preseed/url=${preseed_url}${option}.cfg
initrd ${url}/initrd.gz
boot

:netboot.xyz
chain --autofree http://boot.netboot.xyz

:shell
shell
goto menu

:error
echo Something went wrong...
prompt Press any key to continue.
goto menu
